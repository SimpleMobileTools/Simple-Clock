plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'kotlin-kapt'
}

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    compileSdkVersion 30

    defaultConfig {
        applicationId "com.simplemobiletools.clock"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 24
        versionName "5.6.0"
        setProperty("archivesBaseName", "clock")
        vectorDrawables.useSupportLibrary = true
    }

    signingConfigs {
        if (keystorePropertiesFile.exists()) {
            release {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            }
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (keystorePropertiesFile.exists()) {
                signingConfig signingConfigs.release
            }
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs = ["-XXLanguage:+NewInference"]
    }
}

dependencies {
    implementation 'androidx.constraintlayout:constraintlayout:2.1.2'
    implementation "androidx.preference:preference-ktx:1.1.1"
    implementation "androidx.work:work-runtime-ktx:2.6.0"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.0'

    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.3.1'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'

    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    implementation 'com.github.SimpleMobileTools:Simple-Commons:6cc19172f0'
    implementation 'com.facebook.stetho:stetho:1.5.0'
    implementation 'com.shawnlin:number-picker:2.4.6'
    implementation 'org.greenrobot:eventbus:3.2.0'




}
